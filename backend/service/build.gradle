plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.4'
    id 'io.spring.dependency-management' version '1.1.4'
}

bootJar { enabled = false }
jar { enabled = false }

allprojects {
    repositories {
        mavenCentral()
    }
}

subprojects {
    group = 'com.mung'
    version = '0.0.1-SNAPSHOT'
    sourceCompatibility = '17'

    apply plugin: "java"
    apply plugin: "org.springframework.boot"
    apply plugin: "io.spring.dependency-management"

    bootJar { enabled = true }
    jar { enabled = false }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
        configureEach {
            exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
        }
    }

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-web'
        compileOnly 'org.projectlombok:lombok'
        developmentOnly 'org.springframework.boot:spring-boot-devtools'
        annotationProcessor 'org.projectlombok:lombok'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'

        // mapstruct
        implementation 'org.mapstruct:mapstruct:1.5.3.Final'
        annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.3.Final'
        // log
        implementation 'org.springframework.boot:spring-boot-starter-log4j2'
        implementation group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-yaml', version: '2.16.0'
        // testCode h2
        testImplementation 'com.h2database:h2'
        // test
        testImplementation platform('org.junit:junit-bom:5.9.1')
        testImplementation 'org.junit.jupiter:junit-jupiter'
        // actuator
        implementation 'org.springframework.boot:spring-boot-starter-actuator'
        // postgres
        implementation 'org.postgresql:postgresql'
    }

    tasks.named('test') {
        useJUnitPlatform()
    }
}
